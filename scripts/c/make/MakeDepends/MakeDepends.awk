# MakeDepends.awk
#
# Fix up dependencies generated by gcc with the -MMD option.
# Usage: gawk -v target=$@ -f MakeDepends.awk < $*.dtmp > $*.d

# Match the first line, which contains a colon.
# Note: The /:/ has to be on the same line as the {
/:/ {
	# Convert "filename.o:" to "dirname/filename.o".
	# I don't know why the preprocessor trims off the dirname...
	sub(/^[^:]*:/, target ":")
}

# Match all lines.
{
	# Split on spaces, into the array "words".
	split($0, words)

	# We want to create a command-less, prerequisite-less target
	# for each prerequisite. That way, if we change the name of a header
	# file, GNU make won't complain that it's missing, and it'll
	# rebuild the dependent .o.
	for (val in words)
	{
		# Ignore the trailing "\" line-continuation character.
		# Ignore the target (e.g. "dirname/filename.o:") in the
		# first line.
		if (!(words[val] ~ /\\/ || words[val] ~ /:/))
		{
			# Concatenate to the `prereqs' string.
			prereqs = (prereqs words[val] ":\n")
		}
	}
	print
}

END { print prereqs }
